buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        google()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    dependencies {
        classpath "gradle.plugin.org.jlleitschuh.gradle:ktlint-gradle:3.0.1"
        classpath "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.sonarqube" version "2.6"
}

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'eclipse'
    apply plugin: 'idea'
}

configure(subprojects) {
    apply plugin: 'java'
    apply plugin: 'kotlin'
    apply plugin: "org.jlleitschuh.gradle.ktlint"
    sourceCompatibility = 1.6
}

subprojects {
    version = '0.0.1-SNAPSHOT'
    ext.appName = 'Colonize'
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url "https://plugins.gradle.org/m2/" }
    }
}

// Clearing Eclipse project data in root folder:
tasks.eclipse.doLast {
    delete '.project'
    delete '.classpath'
    delete '.settings/'
}

// Run `gradle pack` task to generate skin.atlas file at assets/ui.
import com.badlogic.gdx.tools.texturepacker.TexturePacker

task pack {
    doLast {
        // Note that if you need multiple atlases, you can duplicate the
        // TexturePacker.process invocation and change paths to generate
        // additional atlases with this task.
        TexturePacker.process(
                'raw/ui',           // Raw assets path.
                'assets/ui',        // Output directory.
                'skin'              // Name of the generated atlas (without extension).
        )
    }
}
